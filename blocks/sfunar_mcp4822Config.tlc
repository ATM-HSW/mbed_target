%% Copyright 2010 The MathWorks, Inc.
%% Copyright 2014 Dr.O.Hagendorf, HS Wismar
%% MCP822 Modifications by Axel Utech 2014, HS Wismar
%% Copyright 2015 M. Marquardt, HS Wismar

%implements sfunar_mcp4822Config "C"

%include "block_common_includes.tlc"

%% Function: BlockTypeSetup ===============================================
%function BlockTypeSetup(block, system) void
  
  %% Ensure required header files are included
  %<MbedCommonBlockTypeSetup(block, system)>

%endfunction


%% Function: Start ========================================================
%function Start(block, system) Output

  %assign p2_val = LibBlockParameterValue(p1,0)
  %assign p3_val = LibBlockParameterValue(p2,0)
  %assign cs_name = "P" + FEVAL("char",p2_val+64) + "_" + FEVAL("int2str",p3_val-1)
    
  %assign spiport_val = CAST("Number", LibBlockParameterValue(SpiPort,0))

  %if !SLibCodeGenForSim()
    %openfile declbuf
    %%

#if defined(MCP4822_DEVICE)
    #error "Multiple MCP4822 Config blocks not supported"
#endif
#define MCP4822_DEVICE

DigitalOut cs_mcp4822(%<cs_name>);

#define CHANNEL_A 0
#define CHANNEL_B 1
#define CHANNEL_SHIFT 7

#define GAIN_1X 1
#define GAIN_2X 0
#define GAIN_SHIFT 5

#define POWER_ON 1
#define POWER_OFF 0
#define POWER_SHIFT 4

void mcp4822_write(uint8_t channel, uint8_t gain, uint8_t shutdown, uint16_t value){
    uint8_t out1 = channel << CHANNEL_SHIFT | gain << GAIN_SHIFT | shutdown << POWER_SHIFT | ((value >> 8) & 0xF);
    uint8_t out2 = value & 0xFF;
    
    cs_mcp4822 = 0;
    spi%<spiport_val>.write(out1);
    spi%<spiport_val>.write(out2);
    cs_mcp4822 = 1;
}

    %%
    %closefile declbuf
    %assign srcFile = LibGetModelDotCFile()
    %<LibSetSourceFileSection(srcFile, "Functions", declbuf)>
    cs_mcp4822=1;
  %endif
%endfunction
