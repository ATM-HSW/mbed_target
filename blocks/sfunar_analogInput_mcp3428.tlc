%%
%% Copyright 2010 The MathWorks, Inc.
%% Copyright 2014 Dr.O.Hagendorf, HS Wismar
%% MCP3428 Modifications by Axel Utech 2014, HS Wismar

%implements sfunar_analogInput_mcp3428 "C"

%include "block_common_includes.tlc"

%% Function: BlockTypeSetup ===============================================
%function BlockTypeSetup(block, system) void

  %% Ensure required header files are included
  %<MbedCommonBlockTypeSetup(block, system)>

%endfunction


%% Function: BlockInstanceSetup ===========================================
%function BlockInstanceSetup(block, system) void
  %%
%%  %<LibBlockSetIsExpressionCompliant(block)> 
  %%
%endfunction


%% Function: Start ========================================================
%function Start(block, system) Output

  %if !SLibCodeGenForSim()
    #if !defined(MCP3428_ADDR_DEVICE)
        #error "No MCP3428 defined! Add a \"MCP3428-Config\" Block!"
    #endif
  %endif
%endfunction


%% Function: Outputs ======================================================
%function Outputs(block, system) Output

  %if !SLibCodeGenForSim()
      %assign p1_val = LibBlockParameterValue(p1, 0)
      %assign p2_val = LibBlockParameterValue(p2, 0)
      %assign p3_val = LibBlockParameterValue(p3, 0)
      %assign errorOutputEnable = LibBlockParameterValue(p4, 0)

      %assign name = FEVAL("strrep",FEVAL("strrep",LibGetFormattedBlockPath(block),"/","_"), " ", "_")
      %%
        uint16_t %<name>_data;
        uint8_t %<name>_conf;
        bool %<name>_success;
        %<name>_success = mcp3428_oneShot(%<p1_val>, %<p2_val>-1, %<p3_val>-1, &%<name>_data, &%<name>_conf);
    %if !LibBlockOutputSignalIsExpr(0)
      %assign y1_val = LibBlockOutputSignal(0, "", "", 0)
        %<y1_val> = %<name>_data;
    %endif 
    %if !LibBlockOutputSignalIsExpr(1)
      %assign y2_val = LibBlockOutputSignal(1, "", "", 0)
        %<y2_val> = mcp3428_calculateVoltage(%<name>_data, %<name>_conf);
    %endif 
    %if !LibBlockOutputSignalIsExpr(2)
      %assign y3_val = LibBlockOutputSignal(2, "", "", 0)
        %<y3_val> = mcp3428_calculateRelative(%<name>_data, %<name>_conf);
    %endif 
    %if errorOutputEnable && !LibBlockOutputSignalIsExpr(3)
      %assign y4_val = LibBlockOutputSignal(3, "", "", 0)
        %<y4_val> = !%<name>_success;
    %else
        (void)%<name>_success;
    %endif 
      %%
  %endif
%endfunction
