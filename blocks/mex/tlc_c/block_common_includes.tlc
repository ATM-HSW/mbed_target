%% File: block_common_includes.tlc
%%
%%  MbedTarget Simulink target
%%  Copyright 2002-2010 The MathWorks, Inc.
%%  Copyright (c) 2014-2020 Dr.O.Hagendorf , HS Wismar
%%  Copyright 2015 M. Marquardt, HS Wismar
%%
%%  Licensed under the Apache License, Version 2.0 (the "License");
%%  you may not use this file except in compliance with the License.
%%  You may obtain a copy of the License at
%%
%%      http://www.apache.org/licenses/LICENSE-2.0
%%
%%  Unless required by applicable law or agreed to in writing, software
%%  distributed under the License is distributed on an "AS IS" BASIS,
%%  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
%%  See the License for the specific language governing permissions and
%%  limitations under the License.

%function SLibCodeGenForSim() void
  %if (CompiledModel.TargetStyle=="SimulationTarget")
    %return 1
  %else
    %return 0
  %endif
%endfunction

%function MbedCommonBlockTypeSetup(block, system) void
  %if EXISTS("_DONE_COMMON_BLOCK_TYPE_SETUP_") == 0
    %assign ::_DONE_COMMON_BLOCK_TYPE_SETUP_ = 1

    %if !SLibCodeGenForSim()
      %if ExtMode
        %% Remove double re-definition in Integer-only case
        %<LibAddToCommonIncludes("remDouble.h")>
      %endif

      %<LibAddToCommonIncludes("mbed.h")>

      %if UseMbedRTOS
        %<LibAddToCommonIncludes("rtos.h")>
      %endif

      %assign srcFile = LibGetModelDotCFile()

      %if (MbedTarget == "mbed_grt")

        %openfile tmpBuf
        void (*stepFctPtr)(void) = &%<LibGetModelName()>_step;
        void (*initFctPtr)(void) = &%<LibGetModelName()>_initialize;
        void (*termFctPtr)(void) = &%<LibGetModelName()>_terminate;
        %closefile tmpBuf

        %<LibSetSourceFileSection(srcFile,"Declarations",tmpBuf)>
      %endif

      %openfile declbuf
      void mymemcpy(void *dest, void *src, size_t n) {
        // Typecast src and dest addresses to (char *) 
        char *csrc = (char *)src; 
        char *cdest = (char *)dest; 

        // Copy contents of src[] to dest[] 
        for (uint32_t i=0; i<n; i++) 
          cdest[i] = csrc[i]; 
      }
      %closefile declbuf

      %assign srcFile = LibGetModelDotCFile()
      %<LibSetSourceFileSection(srcFile, "Functions", declbuf)>

    %endif
  %endif
%endfunction
