%% File : USB_serialRead.tlc
%%
%% Copyright 2010 The MathWorks, Inc.
%% Copyright 2015-2016 Dr.O.Hagendorf, HS Wismar
%% Copyright 2015 M. Marquardt, HS Wismar

%implements USB_serialRead "C"

%include "block_common_includes.tlc"

%% Function: BlockTypeSetup ===============================================
%function BlockTypeSetup(block, system) void

  %% Ensure required header files are included
  %<MbedCommonBlockTypeSetup(block, system)>

%endfunction

%% Function: Outputs ======================================================
%%
%function Outputs(block, system) Output
  %if !SLibCodeGenForSim()
    %assign val_BufferSize = CAST("Number", LibBlockParameterValue(BufferSize,0))
    %assign y1_addr = LibBlockOutputSignalAddr(0, "", "", 0)
    %assign y2_val = LibBlockOutputSignal(1, "", "", 0)

    uint8_T *ptr = %<y1_addr>;
    %<y2_val> = usbSerial.available();

    if(%<y2_val> > 0) {
      if(%<y2_val> <= %<val_BufferSize>) {
        for(uint_T ii=0; ii<%<y2_val>; ii++) {
          *ptr++ = usbSerial._getc();
        }
      }
      else {
        %<y2_val> = %<val_BufferSize>;
        for(uint_T ii=0; ii<%<val_BufferSize>; ii++) {
          *ptr++ = usbSerial._getc();
        }
      }
    }
  %endif
%endfunction
