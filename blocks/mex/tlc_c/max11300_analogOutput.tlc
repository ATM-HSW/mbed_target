%%  MbedTarget Simulink target
%%  Copyright (c) 2014-2017 Dr.O.Hagendorf , HS Wismar
%%
%%  Licensed under the Apache License, Version 2.0 (the "License");
%%  you may not use this file except in compliance with the License.
%%  You may obtain a copy of the License at
%%
%%      http://www.apache.org/licenses/LICENSE-2.0
%%
%%  Unless required by applicable law or agreed to in writing, software
%%  distributed under the License is distributed on an "AS IS" BASIS,
%%  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
%%  See the License for the specific language governing permissions and
%%  limitations under the License.

%% File : sfunar_analogOutput_max11300.tlc
%%
%% Copyright 2010 The MathWorks, Inc.
%% Copyright 2015-2017 Dr.O.Hagendorf, HS Wismar
%% Copyright 2015 M. Marquardt, HS Wismar

%implements max11300_analogOutput "C"

%include "block_common_includes.tlc"

%% Function: BlockTypeSetup ===============================================
%function BlockTypeSetup(block, system) void

  %% Ensure required header files are included
  %<MbedCommonBlockTypeSetup(block, system)>

%endfunction


%% Function: Start ========================================================
%function Start(block, system) Output

  %if !SLibCodeGenForSim()
    %assign csport_val = CAST("Number",LibBlockParameterValue(CsPort, 0))
    %assign cspin_val = CAST("Number",LibBlockParameterValue(CsPin, 0))-1

    %assign pin_val = CAST("Number",LibBlockParameterValue(Pin, 0))-1
    %assign range = CAST("Number",LibBlockParameterValue(Range, 0))

    %assign instance = FEVAL("char",csport_val+64) + FEVAL("int2str",cspin_val)

    max11300_%<instance>.config_pin(%<pin_val>, 5, 0, 0, %<range>, 0, 0);

  %endif
%endfunction


%% Function: Outputs ======================================================
%function Outputs(block, system) Output

  %if !SLibCodeGenForSim()
    %assign csport_val = CAST("Number",LibBlockParameterValue(CsPort, 0))
    %assign cspin_val = CAST("Number",LibBlockParameterValue(CsPin, 0))-1
    %assign instance = FEVAL("char",csport_val+64) + FEVAL("int2str",cspin_val)

    %assign mode_val = CAST("Number",LibBlockParameterValue(Mode, 0))-1

    %assign pin_val = CAST("Number",LibBlockParameterValue(Pin, 0))-1

    %assign u_val = LibBlockInputSignal(0, "", "", 0)

    %if mode_val > 0
      max11300_%<instance>.write_dacRelative(%<pin_val>, %<u_val>);
    %else
      max11300_%<instance>.write_dacRaw(%<pin_val>, %<u_val>);
    %endif

  %endif
%endfunction
