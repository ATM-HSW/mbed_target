%% File : mcp3428.tlc
%%
%% Copyright 2010 The MathWorks, Inc.
%% Copyright 2015-2017 Dr.O.Hagendorf, HS Wismar
%% Copyright 2015 M. Marquardt, HS Wismar

%implements sfunar_analogIn_mcp3428 "C"

%include "block_common_includes.tlc"

%% Function: BlockTypeSetup ===============================================
%function BlockTypeSetup(block, system) void

  %% Ensure required header files are included
  %<MbedCommonBlockTypeSetup(block, system)>

  %<LibAddToCommonIncludes("mcp3428.h")>

  %assign ::MCP3428_instances  = CAST("Number",0)

%endfunction


%% Function: BlockInstanceSetup ===========================================
%function BlockInstanceSetup(block, system) void
  %%
  %<LibBlockSetIsExpressionCompliant(block)>
  %%
%endfunction


%% Function: Start ========================================================
%function Start(block, system) Output
  %if !SLibCodeGenForSim()

    %assign i2cport_val = CAST("Number",LibBlockParameterValue(I2cPort, 0))
    %assign subaddress_val = CAST("Number",LibBlockParameterValue(SubAddress, 0))-1

    %assign i2c_name = "i2c" + FEVAL("int2str",%<i2cport_val>)
    %assign name = FEVAL("strrep",FEVAL("strrep",LibGetFormattedBlockPath(block),"/","_"), " ", "_")

    %if !(::MCP3428_instances & (1<<subaddress_val))
      %openfile declbuf

       MCP3428 mcp3428_%<subaddress_val>(&%<i2c_name>,%<subaddress_val>);

      %closefile declbuf
      %assign srcFile = LibGetModelDotCFile()
      %<LibSetSourceFileSection(srcFile, "Declarations", declbuf)>

      %assign ::MCP3428_instances = ::MCP3428_instances | (1<<subaddress_val)
    %endif

  %endif
%endfunction


%% Function: Outputs ======================================================
%function Outputs(block, system) Output
  %if !SLibCodeGenForSim()

    %assign channel_val = CAST("Number",LibBlockParameterValue(Channel, 0)) - 1
    %assign mode_val = CAST("Number",LibBlockParameterValue(Mode, 0))
    %assign resolution_val = CAST("Number",LibBlockParameterValue(Resolution, 0))-1
    %assign gain_val = CAST("Number",LibBlockParameterValue(Gain, 0))-1
    %assign enableerror_val = CAST("Number",LibBlockParameterValue(EnableError, 0))
    %assign subaddress_val = CAST("Number",LibBlockParameterValue(SubAddress, 0))-1

    %switch mode_val
    %case 1
      %if enableerror_val == 1
        %<LibBlockOutputSignal(1, "", "", 0)> = mcp3428_%<subaddress_val>.read_raw(%<channel_val>, %<resolution_val>, %<gain_val>, %<LibBlockOutputSignalAddr(0, "", "", 0)>, 0);
      %else
        mcp3428_%<subaddress_val>.read_raw(%<channel_val>, %<resolution_val>, %<gain_val>, %<LibBlockOutputSignalAddr(0, "", "", 0)>, 0);
      %endif
      %break

    %case 2
      %if enableerror_val == 1
       %<LibBlockOutputSignal(1, "", "", 0)> = mcp3428_%<subaddress_val>.read_voltage(%<channel_val>, %<resolution_val>, %<gain_val>, %<LibBlockOutputSignalAddr(0, "", "", 0)>, 0);
      %else
        mcp3428_%<subaddress_val>.read_voltage(%<channel_val>, %<resolution_val>, %<gain_val>, %<LibBlockOutputSignalAddr(0, "", "", 0)>, 0);
      %endif
      %break


    %case 3
      %if enableerror_val == 1
        %<LibBlockOutputSignal(1, "", "", 0)> = mcp3428_%<subaddress_val>.read_relative(%<channel_val>, %<resolution_val>, %<gain_val>, %<LibBlockOutputSignalAddr(0, "", "", 0)>, 0);
      %else
        mcp3428_%<subaddress_val>.read_relative(%<channel_val>, %<resolution_val>, %<gain_val>, %<LibBlockOutputSignalAddr(0, "", "", 0)>, 0);
      %endif
      %break
    %endswitch

  %endif
%endfunction
