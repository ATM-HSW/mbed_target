%% File : RTOSmailSend.tlc
%%
%% Copyright 2010 The MathWorks, Inc.
%% Copyright 2015-16 Dr.O.Hagendorf, HS Wismar
%% Copyright 2015 M. Marquardt, HS Wismar

%implements RTOSmailSend "C"

%include "block_common_includes.tlc"

%% Function: BlockTypeSetup ===============================================
%function BlockTypeSetup(block, system) void

  %% Ensure required header files are included
  %<MbedCommonBlockTypeSetup(block, system)>

%endfunction

%% Function: Start ========================================================
%function Start(block, system) Output
  %if !SLibCodeGenForSim()

    %assign valMailNumber = CAST("Number",LibBlockParameterValue(MailNumber, 0))
    %assign valDataType = LibBlockParameterValue(DataType, 0)
    %assign valNumElements = CAST("Number",LibBlockParameterValue(NumElements, 0))
    %assign valDepth = CAST("Number",LibBlockParameterValue(Depth, 0))

    %switch valDataType
      %case 1
        %assign type = "real_T"
        %assign len = 8*valNumElements
        %break
      %case 2
        %assign type = "real32_T"
        %assign len = 4*valNumElements
        %break
      %case 3
        %assign type = "int8_T"
        %assign len = 1*valNumElements
        %break
      %case 4
        %assign type = "uint8_T"
        %assign len = 1*valNumElements
        %break
      %case 5
        %assign type = "int16_T"
        %assign len = 2*valNumElements
        %break
      %case 6
        %assign type = "uint16_T"
        %assign len = 2*valNumElements
        %break
      %case 7
        %assign type = "int32_T"
        %assign len = 4*valNumElements
        %break
      %case 8
        %assign type = "uint32_T"
        %assign len = 4*valNumElements
        %break
    %endswitch

    %assign srcFile = LibGetModelDotCFile()

    %openfile declbuf
    typedef %<type> mail%<valMailNumber>_T[%<valNumElements>];
    %closefile declbuf

    %<LibSetSourceFileSection(srcFile, "IntrinsicTypes", declbuf)>

    %openfile declbuf
    const int mail%<valMailNumber>_len = %<len>;
    Mail<mail%<valMailNumber>_T,%<valDepth>> mail_box%<valMailNumber>;
    %closefile declbuf

    %<LibSetSourceFileSection(srcFile, "Declarations", declbuf)>

  %endif
%endfunction


%% Function: Outputs ========================================================
%function Outputs(block, system) Output
  %if !SLibCodeGenForSim()

    %assign valMailNumber = CAST("Number",LibBlockParameterValue(MailNumber, 0))
    %assign valTimeout = LibBlockParameterValue(Timeout, 0)
    %assign valEnableError = LibBlockParameterValue(EnableError, 0)

    %assign u_ptr = LibBlockInputSignalAddr(0, "", "", 0)
    %if valEnableError == 1
      %assign y1_val = LibBlockOutputSignal(0, "", "", 0)
    %endif

    mail%<valMailNumber>_T *mail = mail_box%<valMailNumber>.alloc(%<valTimeout>);
    if (mail != NULL) {
      memcpy(mail, %<u_ptr>, mail%<valMailNumber>_len);
      mail_box%<valMailNumber>.put(mail);
      %if valEnableError == 1
      %<y1_val> = 0;
      %endif
    }
    %if valEnableError == 1
    else
      %<y1_val> = 1;
    %endif

  %endif
%endfunction