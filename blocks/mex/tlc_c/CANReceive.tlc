%% File : CANReceive.tlc
%%
%% Copyright 2010 The MathWorks, Inc.
%% Copyright 2017 E.Warnicke, HS Wismar
%% Copyright 2017 Dr.O.Hagendorf, HS Wismar

%implements CANReceive "C"

%include "block_common_includes.tlc"

%% Function: BlockTypeSetup ===============================================
%function BlockTypeSetup(block, system) void

  %% Ensure required header files are included
  %<MbedCommonBlockTypeSetup(block, system)>

%endfunction


%% Function: Start ========================================================
%function Start(block, system) Output
  %if !SLibCodeGenForSim()

    %assign CAN_num = CAST("Number",LibBlockParameterValue(CANInstanceID,0))

    %openfile declbuf
    // %<Type> (CANReceive): '%<Name>'
    char can_buffer%<CAN_num>[]={0x00,0x00,0x00,0x00,0x00,0x00,0x00,0x00};
    uint16_T  can_id%<CAN_num>=0;
    int can_len%<CAN_num>=1;
    int can_new%<CAN_num>=-1;
    CANMessage can_rcv_msg%<CAN_num>;
    %closefile declbuf

    %assign srcFile = LibGetModelDotCFile()
    %<LibSetSourceFileSection(srcFile, "Declarations", declbuf)>

    %openfile declbuf
    // %<Type> (CANReceive): '%<Name>'
    void can_rcv_handler%<CAN_num>(){
      can%<CAN_num>.read(can_rcv_msg%<CAN_num>);
      can_new%<CAN_num>=1;
    }
    %closefile declbuf

    %<LibSetSourceFileSection(srcFile, "Functions", declbuf)>

    // %<Type> (CANReceive): '%<Name>'
    can%<CAN_num>.attach(&can_rcv_handler%<CAN_num>);
  %endif
%endfunction


%% Function: Outputs ======================================================
%function Outputs(block, system) Output
  %if !SLibCodeGenForSim()
    %if !LibBlockOutputSignalIsExpr(0)
      %assign CAN_num = CAST("Number",LibBlockParameterValue(CANInstanceID,0))
      %assign msg_new = LibBlockOutputSignal(0, "", "",0) %% new message flag
      %assign msg_id = LibBlockOutputSignal(1, "", "",0) %% message id
      %assign y_ptr = LibBlockOutputSignalAddr(2, "", "", 0) %% message data
      %assign msg_len = LibBlockOutputSignal(3, "", "", 0) %% message length

      if(can_new%<CAN_num> > 0) {
        can_len%<CAN_num> = can_rcv_msg%<CAN_num>.len;
        can_id%<CAN_num> = can_rcv_msg%<CAN_num>.id;
        memcpy(can_buffer%<CAN_num>, can_rcv_msg%<CAN_num>.data, can_rcv_msg%<CAN_num>.len);
        memcpy(%<y_ptr>, can_rcv_msg%<CAN_num>.data, can_rcv_msg%<CAN_num>.len);
        %<msg_len> = can_rcv_msg%<CAN_num>.len;
        %<msg_id>  = can_rcv_msg%<CAN_num>.id;
        %<msg_new> = 1;
        can_new%<CAN_num> = 0;
      } else if(can_new%<CAN_num> < 0) {
        memcpy(%<y_ptr>, can_buffer%<CAN_num>, 1);
        %<msg_len> = 0;
        %<msg_id>  = 0;
        %<msg_new> = 0;
        can_new%<CAN_num> = 0;
      } else {
        memcpy(%<y_ptr>, can_buffer%<CAN_num>, can_len%<CAN_num>);
        %<msg_len> = can_len%<CAN_num>;
        %<msg_id>  = can_id%<CAN_num>;
        %<msg_new> = 0;
      }
    %endif
  %endif
%endfunction