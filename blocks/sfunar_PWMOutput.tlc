%% File : sfunar_PWMOutput.tlc
%%
%% Copyright 2010 The MathWorks, Inc.
%% Copyright 2015 Dr.O.Hagendorf, HS Wismar

%implements sfunar_PWMOutput "C"

%include "block_common_includes.tlc"

%% Function: BlockTypeSetup ===============================================
%function BlockTypeSetup(block, system) void

  %% Ensure required header files are included
  %<MbedCommonBlockTypeSetup(block, system)>

%endfunction

%% Function: Start ========================================================
%function Start(block, system) Output

  %assign p1_val = LibBlockParameterValue(p1, 0)
  %assign p2_val = LibBlockParameterValue(p2, 0)
  %assign p3_val = LibBlockParameterValue(p3, 0)

  %assign name = FEVAL("strrep",LibGetFormattedBlockPath(block),"/","_")
  %assign name = FEVAL("strrep",name," ","_")
  %assign pname = "P" + FEVAL("char",p1_val+64) + "_" + FEVAL("int2str",p2_val-1)

  %if !SLibCodeGenForSim()
    %%
    %<name>.period_us(%<p3_val>);
    %%
  %endif

  %if !SLibCodeGenForSim()
      %openfile declbuf

      PwmOut %<name>(%<pname>);

      %closefile declbuf
      %assign srcFile = LibGetModelDotCFile()
      %<LibSetSourceFileSection(srcFile, "Declarations", declbuf)>
  %endif
%endfunction


%% Function: Outputs ======================================================
%%
%function Outputs(block, system) Output

  %if !SLibCodeGenForSim()
    %assign u1_val = LibBlockInputSignal(0, "", "", 0)

    %assign name = FEVAL("strrep",LibGetFormattedBlockPath(block),"/","_")
    %assign name = FEVAL("strrep",name," ","_")
    %%
    %<name>.pulsewidth_us(%<u1_val>);

  %endif
%endfunction
